MODULE FUNCT
CONTAINS
!TO GET THE MAX OF THIS FUNCTION, WE SHOULD MULTIPLY THIS BY -1, DUE THAT DFP ALGORITHM SEARCHES THE MINIMUM. GRADIENT TOO.

SUBROUTINE ERRVECT(T,P,Q,Y,TH,ER)
IMPLICIT NONE 
!ER_T=Y(T)-TH(P+Q+1)-AR-MA
!TH=(PHI_1, ... , PHI_P, THETA_1,...,THETA_Q,C).
INTEGER, INTENT(IN):: T, P, Q
REAL*8,INTENT(IN):: Y(1:T), TH(1:P+Q+1)	!CONSTANTE=TH(P+Q+1).
REAL*8, INTENT(OUT):: ER(1:T)
INTEGER:: I, J, K
REAL*8:: AR, MA
ER=0D0
DO I=P+1,T !1		!LASO PRINCIPAL.
	AR=0.D0; MA=0.D0		
	DO J=1 , P		!PARTE AUTO-REGRESIVA DEL ERROR.
		AR=AR+TH(J)*Y(I-J)
	END DO 	
	IF (Q .LE. P) THEN	!PARTE DE MEDIA MÓVIL.
		DO K=1,Q
			MA=MA+TH(K+P)*ER(I-K)
		END DO
	ELSE
		DO  K=1,P
			MA=MA+TH(K+P)*ER(I-K)
		END DO
		DO K=P+1,Q
			IF (I-K .LT. 1) EXIT
			MA=MA+TH(K+P)*ER(I-K)			
		END DO	
	END IF
	ER(I)=Y(I)-TH(P+Q+1)-AR-MA
END DO		!1
END SUBROUTINE ERRVECT
!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
DOUBLE PRECISION FUNCTION FUNC(T,P,Q,Y,TH)
IMPLICIT NONE 
!TH=(PHI_1, ... , PHI_P, THETA_1,...,THETA_Q,C)
INTEGER, INTENT(IN):: T, P, Q	!TAMAÑO DE LA SERIE TEMPORAL, ORDEN AUTO-REGRESIVO Y ORDEN DE MEDIA MÓVIL RESPECTIVAMENTE.
REAL*8:: EP(T)	!VECTOR DE ERRORES ALEATORIOS.
INTEGER:: I
REAL*8:: SUMAT		
REAL*8, INTENT(IN):: TH(P+Q+1), Y(1:T)	!Y(T) SERIE TEMPORAL.		
SUMAT=0D0
CALL ERRVECT(T,P,Q,Y,TH,EP)
DO I=P+1,T
	SUMAT=SUMAT+EP(I)*EP(I)
END DO
FUNC=SUMAT
END FUNCTION FUNC
!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!DERIVAT.!!
!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SUBROUTINE DFUNC(T,P,Q,Y,P0,G)
IMPLICIT NONE 
!!!P0=(PHI_1, ... , PHI_P, THETA_1,...,THETA_Q,C)
!!!G(I)=(d FUNC)/(d P0(I));	I=1,...,P+Q+1
INTEGER, INTENT(IN):: T, P, Q	!TAMAÑO DE LA SERIE TEMPORAL, ORDEN AUTO-REGRESIVO Y ORDEN DE MEDIA MÓVIL RESPECTIVAMENTE.
REAL*8,INTENT(IN):: Y(T)	!VECTOR DE SERIE TEMPORAL.
REAL*8, INTENT(IN), DIMENSION(P+Q+1):: P0
DOUBLE PRECISION, INTENT(OUT), DIMENSION(1:P+Q+1):: G
REAL*8:: EP(1:T), DC(P+1:T), DPH(P+1:T,1:P), DTH(P+1:T) !EP = VECTOR DE ERRORES ALEATIORIOS; DC = DERIVADAS PARCIALES REFERENTES A C; DPH = DERIVADAS PARCIALES EN FUNCIÓN DE PARÁMETROS AUTO-REGRESIVOS; DTH = DERIVADAS PARCIALES REFERENTES A PARÁMETROS DE MEDIA MÓVIL.
INTEGER:: I,J,K
G=0.D0
CALL ERRVECT(T,P,Q,Y,P0,EP)
!1. G(P+Q+1)=(d FUNC)/(d CONSTANT).
DC=-1D0
DC(P+2)=-1D0+P0(P+1)
G(P+Q+1)=EP(P+1)*DC(P+1)+EP(P+2)*DC(P+2)	!PRIMER COMPONENTE DE G(P+Q+1).
DO I=P+3,T	!2	!ELEMENTOS DE G(P+Q+1).
	IF (I .LE. P+Q) THEN 
		DO J=1, I-P-1 !2.1
			DC(I)=DC(I)-P0(P+J)*DC(I-J)
		END DO		!2.1
	ELSE
		DO J=1,Q	!2.2
			DC(I)=DC(I)-P0(P+J)*DC(I-J)
		END DO		!2.2
	END IF
	G(P+Q+1)=G(P+Q+1)+EP(I)*DC(I)		
END DO			!2
!2. G(I)=(d FUNC)/(d P0(I));	I=1,...,P.
DO I=P+1,T !3
	DO J=1,P !3.1	
		DPH(I,J)=-Y(I-J)
	END DO	 !3.1
END DO		 !3
DO J=1,P		!4
	DO I=P+2,T		!4.1
		IF (I .LE. P+Q) THEN 
			DO K=1,I-P-1 !4.1.1
				DPH(I,J)=DPH(I,J)-P0(K+P)*DPH(I-K,J)
			END DO			 !4.1.1
		ELSE
			DO K=1,Q 		 !4.1.2
				DPH(I,J)=DPH(I,J)-P0(P+K)*DPH(I-K,J)
			END DO 		 	 !4.1.2
		END IF
	END DO				!4.1
END DO			!4
DO I=1,P !1
	DO J=P+1,T	!1.1
		G(I)=G(I)+EP(J)*DPH(J,I)
	END DO			!1.1
END DO	 !1
!3. G(I)=(d FUNC)/(d P0(I));	I=P+1,...,P+Q
DTH=0D0
DTH(P+2)=-EP(P+1)
DTH(P+3)=-EP(P+2)+P0(P+1)*EP(P+1)
DO I=P+4,T !6
	DTH(I)=-EP(I-1)	
	IF (I .LE. P+Q) THEN 
		DO K=1,I-P-1 !6.1
			DTH(I)=DTH(I)-P0(P+K)*DTH(I-K)
		END DO			 !6.1
	ELSE
		DO K=1,Q		 !6.2
			DTH(I)=DTH(I)-P0(P+K)*DTH(I-K)
		END DO 		 	 !6.2
	END IF
END DO		 !6
DO I=1,Q !1
	DO J=P+I,T	!1.1
		G(P+I)=G(P+I)+EP(J)*DTH(J-I+1)
	END DO			!1.1
END DO	 !1
!4. G(I) 
G=2D0*G	!G=-2*G, PARA MAXIMIZAR SE MULTIPLICA POR -1.
END SUBROUTINE DFUNC
!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!	
!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
SUBROUTINE DFUN(T,P,Q,Y,P0,G)
IMPLICIT NONE
REAL*8, PARAMETER:: DELTA = 1.D-8
INTEGER, INTENT(IN):: T, P, Q
REAL*8,INTENT(IN):: Y(T)
REAL*8, INTENT(IN), DIMENSION(1:P+Q+1):: P0
DOUBLE PRECISION, INTENT(OUT), DIMENSION(1:P+Q+1):: G
INTEGER:: I
REAL*8::AUX, AUXV(P+Q+1,P+Q+1), INDELTA
INDELTA=(1D0/DELTA)
AUXV=0D0
AUX=FUNC(T,P,Q,Y,P0)
DO I=1, P+Q+1
	AUXV(:,I)=P0
	AUXV(I,I)=AUXV(I,I)+DELTA
	G(I)=FUNC(T,P,Q,Y,AUXV(:,I))-AUX
END DO
G=INDELTA*G
END SUBROUTINE DFUN

END MODULE FUNCT
